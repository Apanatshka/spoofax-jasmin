module binding

imports

	include/JasminXT
	trans/analysis/extra-constructors
	
namespaces

	Class
	Method
	Variable
	Label 
	
binding rules

  JBCFile(JBCHeader(_, _, JBCClass(_, c), JBCSuper(p), _, _, _, _), _, _):
    defines Class c
    of type p
    scopes Method
  
  JBCSuper(c):
    imports 
      Method, 
      imported Method 
    from Class c
    
  CRef(c):
  	refers to Class c
  	
binding rules
  
	JBCMethod(_, name-and-desc, _): 
		defines Method name-and-desc
		scopes Label, Variable
	
	JBCMethodRef(CRef(c), name-and-desc):
		refers to Method name-and-desc
		in Class c

binding rules
    
  JBCVarDecl(n, _, _, _, _): 
    defines Variable n
  
  VarNum(n): 
    refers to Variable n

binding rules
	
	JBCLabel(l): 
		defines Label l
	
	LabelRef(l): 
		refers to Label l
